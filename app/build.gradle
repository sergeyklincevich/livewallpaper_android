apply plugin: 'com.android.application'

configurations {
    apt
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    defaultConfig {
        applicationId "com.sergeyklincevich.androidlivewallpaper"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'

    def androidAnnotationsVer = '3.0'
    apt "org.androidannotations:androidannotations:$androidAnnotationsVer"
    compile "org.androidannotations:androidannotations-api:$androidAnnotationsVer"

}

def getSourceSetName(variant) {
    return new File(variant.dirName).getName();
}

android.applicationVariants.all { variant ->

    def aptOutputDir = project.file("build/generated/source/apt")
    def aptOutput = new File(aptOutputDir, variant.dirName)

    android.sourceSets[getSourceSetName(variant)].java.srcDirs += aptOutput.getPath()

    variant.javaCompile.options.compilerArgs += [
            '-processorpath', configurations.apt.getAsPath(),
            '-AandroidManifestFile=' + variant.outputs[0].processResources.manifestFile,
            '-AresourcePackageName=com.sergeyklincevich.androidlivewallpaper',
            '-Atrace',
            '-s', aptOutput
    ]

    variant.javaCompile.source = variant.javaCompile.source.filter { p ->
        return !p.getPath().startsWith(aptOutputDir.getPath())
    }

    variant.javaCompile.doFirst {
        aptOutput.mkdirs()
    }
}

